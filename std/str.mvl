include "mem.mvl"

proc strlen(str: s64) -> s64:
  i = 0

  ptr = str + i
  char = *ptr

  while char != 0s8:
    ptr = str + i
    char = *ptr
    i = i + 1
  end

  retval i
end

proc streq(a: s64, b: s64) -> s64:
  a_len = strlen(a)
  b_len = strlen(b)

  if a_len != b_len:
    retval 0
  end

  i = 0
  while i < a_len:
    a_ptr = a + i
    a_char = *a_ptr
    b_ptr = b + i
    b_char = *b_ptr
    if a_char != b_char:
      retval 0
    end

    i = i + 1
  end

  retval 1
end

proc s64_to_str(num: s64) -> s64:
  buf_cap = 1
  buf_size = 0
  size_to_alloc = buf_cap + 1
  buf = malloc(size_to_alloc)
  is_neg = 0

  if num < 0:
    *buf = '-'
    buf_size = buf_size + 1
    is_neg = 1
    num = -num
  end

  while 1 == 1:
    if buf_size >= buf_cap:
      buf_cap = buf_cap * 2
      size_to_alloc = buf_cap + 1
      buf = realloc(buf, size_to_alloc)
    end

    digit = num % 10
    char = digit + '0'
    char_s8 = cast s8 char

    ptr = buf + buf_size
    *ptr = char_s8

    num = num / 10
    buf_size = buf_size + 1

    if num == 0:
      break
    end
  end

  i = is_neg
  j = buf_size - 1
  while i < j:
    ptr_a = buf + i
    ptr_b = buf + j
    temp_a = *ptr_a
    temp_b = *ptr_b
    *ptr_a = temp_b
    *ptr_b = temp_a

    i = i + 1
    j = j - 1
  end

  ptr = buf + buf_size
  *ptr = 0s8

  retval buf
end

proc str_to_s64(str: s64) -> s64:
  num = 0
  is_neg = 0
  i = 0

  ptr = str
  char = *str

  if char == 0s8:
    retval 0
  end

  if char == 45s8: # 45 = '-'
    is_neg = 1
    i = 1
  end

  while 1 == 1:
    ptr = str + i
    char = *ptr

    zero = '0'
    zero_s8 = cast s8 zero
    if char < zero_s8:
      break
    end

    nine = '9'
    nine_s8 = cast s8 nine
    if char > nine_s8:
      break
    end

    digit = char - zero_s8
    digit_s64 = cast s64 digit

    num = num * 10
    num = num + digit_s64

    i = i + 1
  end

  if is_neg == 1:
    num = -num
  end

  retval num
end
